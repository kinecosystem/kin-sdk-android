apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'

// maven plugin and group definition
// needed for jitpack support
apply plugin: 'maven'
group = 'com.github.kinecosystem'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        consumerProguardFiles 'proguard-rules.pro'
        testInstrumentationRunner "kin.sdk.MultiDexAndroidJUnitRunner"
        multiDexEnabled true
    }

    buildTypes {
        debug {
            testCoverageEnabled true
            buildConfigField "String", "INTEG_TESTS_NETWORK_FRIENDBOT", "\"${System.getenv('INTEG_TESTS_NETWORK_FRIENDBOT') ?: ""}\""
            buildConfigField "String", "INTEG_TESTS_NETWORK_PASSPHRASE", "\"${System.getenv('INTEG_TESTS_NETWORK_PASSPHRASE') ?: ""}\""
            buildConfigField "String", "INTEG_TESTS_NETWORK_URL", "\"${System.getenv('INTEG_TESTS_NETWORK_URL') ?: ""}\""
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
    implementation project(':kin-sdk:kin-base')

    implementation 'com.android.support:support-annotations:28.0.0'

    implementation 'com.github.joshjdevl.libsodiumjni:libsodium-jni-aar:2.0.1'
    api 'com.github.kinecosystem:kin-utils-android:1.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.10.0'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.1'
    testImplementation 'org.robolectric:robolectric:4.3'
    testImplementation 'com.google.code.gson:gson:2.8.2'
    testImplementation 'org.json:json:20180813'
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation project(':kin-sdk:kin-base')

    androidTestImplementation 'junit:junit:4.12'
    androidTestImplementation 'org.mockito:mockito-core:2.10.0'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'org.mockito:mockito-android:2.10.0'
    androidTestImplementation 'org.mockito:mockito-android:2.10.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'com.android.support:multidex:1.0.2'
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

//bundle javadocs with published aar
task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.implementation
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
artifacts {
    archives sourcesJar
}

//jacoco unified code coverage
jacoco {
    toolVersion = '0.8.2'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport) {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', '**/*coverage.ec'
    ])
}

//remove all non test related from kotlin source set, to make sure no kotlin files included in build
afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.contains("test")) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}
