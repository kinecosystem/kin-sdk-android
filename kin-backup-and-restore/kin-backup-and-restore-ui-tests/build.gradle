apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'jacoco'

android {
    compileSdkVersion android_compilesdk_version

    defaultConfig {
        applicationId "kin.backup.and.restore.ui.tests"
        minSdkVersion 18 // This is only 18+ instead of android_minsdk_version b/c of the ui test automator requiring 18+
        targetSdkVersion android_targetsdk_version
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    sourceSets {
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$androidsupport_version"
    implementation "com.android.support.constraint:constraint-layout:$constraintlayout_version"
    api project(':kin-backup-and-restore:kin-backup-and-restore-lib')
    api project(':kin-sdk:kin-sdk-lib')

    testImplementation "junit:junit:$junit_version"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    androidTestImplementation "com.android.support.test:runner:$supporttest_version"
    androidTestImplementation "com.android.support.test:rules:$supporttest_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$espresso_version"
    androidTestImplementation "com.android.support.test.uiautomator:uiautomator-v18:$uiautomator_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

//jacoco unified code coverage
jacoco {
    toolVersion = jaccoco_version
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport) {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "$project.buildDir/intermediates/classes/debug", excludes: fileFilter)
    def testedDebugTree = fileTree(dir: "$project.rootDir/kin-backup-and-restore/kin-backup-and-restore-lib/build" +
            "/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"
    def testedSrc = "$project.rootDir/kin-backup-and-restore/kin-backup-and-restore-lib/src/main/java"

    sourceDirectories = files([testedSrc, mainSrc])
    classDirectories = files([debugTree, testedDebugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', '**/*coverage.ec'
    ])
}

